// Реализация шагов BDD-фич/сценариев c помощью фреймворка https://github.com/artbear/1bdd

Перем БДД; //контекст фреймворка 1bdd

// Метод выдает список шагов, реализованных в данном файле-шагов
Функция ПолучитьСписокШагов(КонтекстФреймворкаBDD) Экспорт
	БДД = КонтекстФреймворкаBDD;

	ВсеШаги = Новый Массив;

	ВсеШаги.Добавить("ЯСоздаюПараметрывыпускаВКонтексте");
	ВсеШаги.Добавить("ЯДобавляюПараметрСИменемВКонтекст");
	ВсеШаги.Добавить("ЯДобавляюПараметрБулевоСИменемВКонтекст");
	ВсеШаги.Добавить("ЯСоздаюНовыйВыпускСПараметрамиВКонтексте");
	ВсеШаги.Добавить("ЯУдаляюВыпускИзКонтекста");

	Возврат ВсеШаги;
КонецФункции

// Реализация шагов

// Процедура выполняется перед запуском каждого сценария
Процедура ПередЗапускомСценария(Знач Узел) Экспорт
	
КонецПроцедуры

// Процедура выполняется после завершения каждого сценария
Процедура ПослеЗапускаСценария(Знач Узел) Экспорт
	
КонецПроцедуры

//Я удаляю выпуск из контекста "Выпуск"
Процедура ЯУдаляюВыпускИзКонтекста(Знач ИмяПеременной) Экспорт
	Выпуск = БДД.ПолучитьИзКонтекста(ИмяПеременной);
	Выпуск.Удалить();
КонецПроцедуры

//Я создаю новый выпуск с параметрами <1.0.1> в контексте "Выпуск"
Процедура ЯСоздаюНовыйВыпускСПараметрамиВКонтексте(Знач ИмяПараметров, Знач ИмяПеременной) Экспорт

	ПараметрыВыпуска = БДД.ПолучитьИзКонтекста(ИмяПараметров);
	
	Метка = ПараметрыВыпуска.Метка;
	Наименование = ПараметрыВыпуска.Наименование;
	Описание = ПараметрыВыпуска.Описание;
	ВеткаИлиКоммит = ПараметрыВыпуска.ВеткаИлиКоммит;
	Черновик = ПараметрыВыпуска.Черновик;
	ПредварительныйВыпуск = ПараметрыВыпуска.ПредварительныйВыпуск;


	ГитХабРепозиторий = БДД.ПолучитьИзКонтекста("ГитХабРепозиторий");
	ПоследнийВыпуск = ГитХабРепозиторий.СоздатьВыпуск(Метка, Наименование, Описание, ВеткаИлиКоммит, Черновик, ПредварительныйВыпуск);
	БДД.СохранитьВКонтекст(ИмяПеременной, ПоследнийВыпуск);
	
КонецПроцедуры

//Я создаю ПараметрыВыпуска в контексте "ПараметрыВыпуска"
Процедура ЯСоздаюПараметрывыпускаВКонтексте(Знач ИмяПеременной) Экспорт
	
	ПараметрыВыпуска = Новый Структура("ВеткаИлиКоммит");

	БДД.СохранитьВКонтекст(ИмяПеременной, ПараметрыВыпуска);

КонецПроцедуры

//Я добавляю параметр <1.0.1> с именем "Метка" в контекст "ПараметрыВыпуска"
Процедура ЯДобавляюПараметрСИменемВКонтекст(Знач ЗначениеПараметра, Знач ИмяПараметра, Знач ИмяПеременной) Экспорт

	ПараметрыВыпуска = БДД.ПолучитьИзКонтекста(ИмяПеременной);
	ПараметрыВыпуска.Вставить(ИмяПараметра, ЗначениеПараметра);
КонецПроцедуры

//Я добавляю параметр булево <Ложь> с именем "Черновик" в контекст "ПараметрыВыпуска"
Процедура ЯДобавляюПараметрБулевоСИменемВКонтекст(Знач ЗначениеПараметра, Знач ИмяПараметра, Знач ИмяПеременной) Экспорт
	ПараметрыВыпуска = БДД.ПолучитьИзКонтекста(ИмяПеременной);
	ПараметрыВыпуска.Вставить(ИмяПараметра, Булево(ЗначениеПараметра));
КонецПроцедуры





